\section{Formal Model and Verification Approach}
\label{sec:approach}

We model the Algorand consensus protocol in Coq as an inductive binary relation on global protocol states, and then express key properties as \emph{invariants} that hold for all protocol states that are (transitively) reachable, according to the relation, from some initial state. Ultimately, we state and prove the \emph{asynchronous safety} property of our model in this way.

Our verification approach is similar to that used for the Raft consensus protocol~\cite{Woos2016} and the Toychain blockchain formalization~\cite{Pirlea2018}, but is different from, for example, the approach based on reasoning on Lamport's happens-before relation used to verify the PBFT protocol~\cite{Rahli2018}. We also elide distributed separation logic, which has been used recently for consensus protocol verification in Coq~\cite{Sergey2018}.

As outlined below, our protocol model, properties, and proofs rely heavily data structures and results from the Mathematical Components project~\cite{MathComp}, and in particular its libraries for finite sets and finite maps~\cite{Cohen2019}. In total, our Coq development contains around 2000 lines of Gallina code for the specification of the model and its properties, and around 4000 lines of proof scripts that build the formal proofs.
%The source files also contain almost 1000 lines of comments that motivate and explain the specifications and lemmas.

%The modeling effort involved primarily:
%\begin{itemize}
%\item Defining all the needed data types and structures, such as message types and records, using the inductive constructions facilitated by the logic underlying Coq. Definitions were designed to utilize various existing infrastructure components, provided as Coq libraries, including most prominently the Mathematical Components (MathComp) library. These definitions specify the structure of the protocol's state.
%\item Defining both: (1) utility functions, such as computing the number of soft-votes seen by a user so far or identifying the proposal with the least credential, and (2) logical propositions asserting, for example, the preconditions for a cert-voting action or whether some event (like cert-voting) had taken place along an execution path. Functions and propositions constitute the basic building blocks using which other more complex (inductive) definitions and statements are formed.
%\item Stating and proving basic structural properties about these definitions to ensure their correctness with respect to their intended behavior, for example, showing that a defined relation is a complete order or that a function that updates user timers preserves the set of users in the system. These properties are crucial for building up other proofs of more elaborate statements. We note that definitions imported from external libraries, such as MathComp, come equipped with many useful properties already proved, which are also heavily used in our model.
%\item Formalizing the argument for safety and proving the safety theorem, which in turn required designing a proof plan and identifying the intermediate results that will need to be shown as proof obligations. This process is iterative, since many intermediate results were deep enough that they required breaking them down into smaller, more manageable pieces. The whole process was guided by the informal proof arguments documented in the protocol description by Algorand[add pointers]. We highlight some of the details of this proof later in the report.
%\end{itemize}

\subsection{Assumptions}

Most formalizations of programs and protocols are parameterized in specific ways, to be valid across a wide range of configuration choices and scenarios that may occur in practice. Our Algorand model is parameterized on a finite set of \emph{user identifiers} (e.g., names of network nodes), and on a finite set of \emph{values} used in message payloads, abstractly representing blocks and block hashes.

\begin{lstlisting}[language=Coq]
Parameters (UserId : finType) (Value : finType).
\end{lstlisting}

Values are assumed to be checkable for validity. We also consider an arbitrary, but totally ordered, datatype of credentials owned by users, and assume there is some way, given a credential, to say whether its owner is a committee member. Finally, we parameterize on delays and Algorand-specific configuration parameters such as number of votes required to move between periods. For example, these real-number parameters represent, respectively, the non-block message delivery delay, the block message delivery day, and the recovery time:
\begin{lstlisting}[language=Coq]
Parameters (lambda : R) (big_lambda : R) (L : R).
\end{lstlisting}

Besides these and other parameters (16 in total), we also express assumptions, most related to our parameters, in the form of axioms. We use four kinds of axioms:
(1) standard axioms from classical logic, (2) axioms about credentials, (3) axioms on limits of message delays, and (4) axioms on the composition of groups (quorums) of users satisfying certain conditions.

We use the first kind of axiom (three in total) to facilitate simpler use of and reasoning about real numbers.
One example of the second kind of axiom (out of two in total), expresses the uniqueness of credentials
across different users:
\begin{lstlisting}[language=Coq]
Axiom credentials_different :
 forall (u u' : UserId) (r r' p p' s s' : nat), u <> u' ->
 credential u r p s <> credential u' r' p' s'.
\end{lstlisting}
In other words, any two distinct user identifiers \lstinline{u} and \lstinline{u'} will have distinct credentials no matter what the round, period and step values are.
The two axioms of the third kind specify how the small message delivery delay \lstinline{lambda}, block message delivery delay \lstinline{big_lambda}, and the recovery time period \lstinline{L} are all related:
\begin{lstlisting}[language=Coq]
Axiom delays_order : (3 * lambda <= big_lambda < L)%R.
Axiom delays_positive : (lambda > 0)%R.
\end{lstlisting}

As one example of the fourth kind of axiom, the assumption that ``any two large-enough cert-voting committees for a given round-period-step triple must share at least one honest user'', which captures the honest supermajority assumption in Algorand for the cert-voting case, is specified as:
\begin{lstlisting}[language=Coq]
Axiom quorums_c_honest_overlap :
 quorum_honest_overlap_statement tau_c.
\end{lstlisting}
where \lstinline{quorum_honest_overlap_statement} is a definition parameterized by the committee size threshold value \lstinline{tau}:
\begin{lstlisting}[language=Coq]
Definition quorum_honest_overlap_statement (tau : nat) :=
 forall (trace : seq GState) (r p s : nat) (q1 q2:{fset UserId}),
  q1 `<=` committee r p s -> #|` q1 | >= tau ->					
  q2 `<=` committee r p s -> #|` q2 | >= tau ->					
  exists (honest_voter : UserId),
   honest_voter \in q1 /\ honest_voter \in q2 /\
   honest_during_step (r,p,s) honest_voter trace.
\end{lstlisting}
In other words, if for any two quorums of users, each of size at least \lstinline{tau}, and who are all committee members for the given \lstinline{(r,p,s)} triple, there is an honest user for the step \lstinline{(r,p,s)} who belongs to both quorums. In total, there are six axioms of this kind.

Our axioms either capture statements accepted by mathematicians (1 above) or assumptions made by Algorand designers on the runtime environment (2-4 above).

\subsection{Local and Global State}

We model local (user) state as an inductive datatype that holds information such as the current round and period, as well as blocks and votes seen during the round.

\begin{lstlisting}[language=Coq]
Record UState := mkUState {
  corrupt : bool;
  round : nat;
  period : nat;
  step : nat;
  timer : R;
  deadline : R;
  p_start : R;
  proposals     : nat -> nat -> seq PropRecord;
  stv           : nat -> option Value;
  blocks        : nat -> seq Value;
  softvotes     : nat -> nat -> seq Vote;
  certvotes     : nat -> nat -> seq Vote;
  nextvotes_open: nat -> nat -> nat -> seq UserId;
  nextvotes_val : nat -> nat -> nat -> seq Vote
}.
\end{lstlisting}

We model the global state as an inductive datatype that holds the current time, whether the network is partitioned, current state for all users, messages in transit, and received messages. In particular, the state of users in the global state is represented as a finite map from user identifier to state, and messages in transit as a finite map from user identifier to multisets of pairs of message deadlines and messages.

\begin{lstlisting}[language=Coq]
Record GState := mkGState {
  now : R;
  network_partition : bool;
  users : {fmap UserId -> UState};
  msg_in_transit : {fmap UserId -> {mset R * Msg}};
  msg_history : {mset Msg}
}.
\end{lstlisting}

\subsection{The Global Transition Relation}
The global state transition relation is defined over the \lstinline{GState} type, and denoted \lstinline{g ~~> g'} in Coq when it holds for \lstinline{g} and \lstinline{g'}. We distinguish three kinds of transitions: (a) node-internal transitions, (b) node message delivery transitions, and (c) network transitions.

Examples of the first transition (12 total) include a user proposing a block or a user certifying a block. There are eight transitions of the second form, such as a user delivering a next-vote for a block and advancing its period. The six network transitions include advancing the users' timers, entering and exiting a partition, and adversarial actions such as replaying a message.

%The global state transition relation is denoted \lstinline{g ~~> g'}, where $g$ and $g'$ are global states of the protocol. This relation defines inductively how the protocol's global state transitions into another state in one step. For example, the global state may transition into another by advancing time, having one of its users make an internal transition or by having the adversary partition the network, and so on. Each transition is guarded by certain preconditions that need to be satisfied for the transition to be enabled. Therefore, the statement that \lstinline{g ~~> g'} tells us that the preconditions for one of these global transition cases were enabled and that transition was taken to transform $g$ into $g'$.

To chain global states together, we define a \emph{trace} as a non-empty sequence of global states, where each adjacent pair belongs to the global transition relation, i.e. if state \lstinline{g$_i$} is the $i$th state in the sequence and \lstinline{g$_{i+1}$} is the $(i+1)$th state, then the pair \lstinline{g$_i$} and \lstinline{g$_{i+1}$} belongs to the global transition relation. 

By specifying path properties as propositions on traces, we are able to define generically what the property is without having to assume a fully concrete initial state. Any conditions required for the property to hold can be specified as constraints on the states of the trace being considered.

For example, the property that a block was certified in a period is captured by the following proposition:
\begin{lstlisting}[language=Coq]
Definition certified_in_period trace (r p:nat) (v:Value) :=
  exists (certvote_quorum : {fset UserId}),
  certvote_quorum `<=` committee r p 3 /\
  #|` certvote_quorum | >= tau_c /\
  forall (voter : UserId), voter \in certvote_quorum ->
   certvoted_in_path trace voter r p v.
\end{lstlisting}
It states that the proposition holds for a trace if there exists a large-enough quorum of users selected for cert-voting who actually published cert-votes along that trace for the given period (the proposition \lstinline{certvoted_in_path}).
